graphics_api_step -> 0.0
graphics_api N ->
    stepping
    picture
        interpolate 0.05, N, graphics_api_step
        graphics_api_picture graphics_api_step

graphics_api_picture S:real ->
    image -210, -160, 60%, 60%, "rampage.png"
    image 200, -250, 20%, 20%, "NVIDIA Tesla P4Card.png"
    image 650, -200, 50%, 50%, "NEC Display.png"
    locally
        line_color "#36B600", fade_between(S, 1, 3)
        color "transparent"
        line_width 5
        rectangle -249, -285, 140, 32
        line_arrow -160, -284, arrowhead, 34, -284, arrowhead
        line_width 10
        line_arrow 400, -284, arrowhead, 534, -284, arrowhead

    locally
        show fade_at(S, 2)
        diagram_box "rgba(120,20,20,210)", 0, -100, 1000, 150 * fade_at(S,2), "Virtualization (Host kernel)"
        diagram_box "#052", -200, -125, 350, 60, "Chipset Drivers"
        locally
            show 1 - fade_between(S, 6, 7)
            diagram_box "#026", 200, -125, 350, 60, "GPU Driver"
        locally
            show fade_at(S, 7)
            diagram_box "#026", 200, -125, 350, 60, "vGPU Driver"

    if S in 2.1..3.9 then
        graphics_api_VMs fade_between(S, 3, 3), 3
    else if S in 4.1..5.9 then
        graphics_api_VMs fade_between(S, 5, 5), 5
    else if S in 6.1..7.9 then
        graphics_api_VMs fade_between(S, 7, 7), 7
    else
        graphics_api_OS S
        graphics_api_app S

graphics_api_app_texture S:real ->
    rotate_time S:real ->
        if S in 1.5..3.5 then
            0.05 * integer (time * 3)
        else
            time
    frame_texture 200, 100,
        background_color "black", 20%
        light 0
        light_position 300, 300, 300
        rotate_x 40
        for I in 1..7 loop
            locally
                rotate_y (20 + 1.3 * I^3) * rotate_time S
                translate_z 10 + 6 * I
                color_hsv 80 * I + 12 * rotate_time S, 60%, 100%
                sphere 30 - 2 * I * sin(0.7 * I + rotate_time S)
    color "white"
    show fade_between(S, 1, 3) + fade_at(S, 4)

graphics_api_app S:real ->
    locally
        graphics_api_app_texture S
        ellipse 200, 260 + 120 * fade_at(S, 2), 200, 100
        locally
            color "#88C"
            translate 200, 80 + 120 * fade_at(S, 2), 15
            rotate_z -90
            arrow 0, 0, 250, 100, 44, 0.32

            color "transparent"
            line_color "#44C"
            line_width 4
            texture ""
            arrow 0, 0, 250, 100, 44, 0.32

            show fade_between(S, 6, 6)
            color "#44C"
            arrow 230 + page_time mod 1 * 75, 0, 100 + page_time mod 1 * 150, 100, 44, 0.32

        if S <> 3 and S <> 5 and S <> 7 then
            rectangle 650, -180, 280, 160

graphics_api_OS S:real ->
    locally
        translate_y 120 * fade_at(S, 2)
        diagram_box "#298", 0, 260, 1000, 150, "Application"
        diagram_box "#026", 0, 100, 1000, 150, "Graphics Environment"
        diagram_box "#A02", 0, -60, 1000, 150,
            if S < 1.5 then
                text "Kernel"
            else
                text "Guest kernel"
        translate_z 30
        diagram_box "#7A7", -200, 80, 350, 60, "Window Manager"
        diagram_box "#77A", 200, 80, 350, 60, "Graphics API"
        diagram_box "#063", -200, -85, 350, 60, "Chipset Drivers"
        if S in 1.5..3.5 then
            diagram_box "#03C", 200, -85, 350, 60, "VGA Driver"
        else if S in 3.5..5.5 then
            diagram_box "#66C", 200, -85, 350, 60, "VIRTIO driver"
        else if S in 6.5..7.5 then
            diagram_box "#038", 200, -85, 350, 60, "vGPU driver"
        else
            diagram_box "#038", 200, -85, 350, 60, "GPU driver"
        translate_z 50
        image 430, 60, 15%, 15%, "DirectX12.png"
        image 430, 110, 10%, 10%, "vulkan.png"
        image 430, 150, 10%, 10%, "opengl.png"
        show 60%
        color "white", fade_at(S, 2)
        image -420, -140, 15%, 15%, "Intel CPU.png"
        image -300, -140, 12%, 12%, "Motherboard.png"
        color "white", fade_between(S, 2, 3)
        image  300, -140, 42%, 42%, "OldATICard.png"
        color "white", fade_between(graphics_api_step, 4, 5)
        image  300, -140, 30%, 30%, "Virtual GPU.png"
        color "white", fade_between(graphics_api_step, 7, 7)
        image  300, -140, 12%, 12%, "NVIDIA Tesla P4Card.png"

graphics_api_VMs S:real, APIIndex:real ->
    locally
        translate -400, 55 * S, 0
        rotate_y -35 * S
        scale 100% - 25% * S
        show 100% - 0% * S
        translate 400, 0, -800 * S
        graphics_api_OS APIIndex
        translate_z 400 * S
        graphics_api_OS APIIndex
        translate_z 400 * S
        graphics_api_OS APIIndex
        graphics_api_app APIIndex
    locally
        graphics_api_app_texture APIIndex
        graphics_vm_texture 0 -> color "white"
        graphics_vm_texture 1 -> color "white", S; texture "vt-d.png"
        graphics_vm_texture 2 -> color "white", S; texture "Civilization.jpg"
        for I in 0..2 loop
            graphics_vm_texture I
            rectangle 650 - (30 - 25*I) * S, -180 + (30 - 25*I) * S, 280 - 60*S, 160 - 60*S
            texture ""
            color "transparent"
            line_color_hsv 50 * I, 80%, 80%, S
            line_width 4
            rectangle 650 - (30 - 25*I) * S, -180 + (30 - 25*I) * S, 280 - 60*S, 160 - 60*S

graphics_api_mark Color:text, Mark:text ->
    picture
        translate 400, -20, 50
        rotate_z 20
        scale 4 + sin(1.5*time)^4
        show fade_at(page_time, 5)
        color Color, 50%
        line_color Color
        font "Impact", 30
        text Mark
