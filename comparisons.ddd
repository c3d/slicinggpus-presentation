// ----------------------------------------------------------------------------
base_slide "Comparison of GPU virtualization solutions",
// ----------------------------------------------------------------------------
    title page_label
    subtitle "Tradeoffs, tradeoffs, it's all about tradeoffs"
    stepping
    picture
        consideration 0, "Hardware requirements"
        consideration 1, "Software requirements"
        consideration 2, "Performance"
        consideration 3, "Security"
        consideration 4, "Flexibility"
        consideration 5, "Scalability"
        virt_type_hsv := 60 * page_number


//  Percentage for...        H/W, S/W, Perf, Sec, Flex, Scal
virt_type "Native GPU",      100, 100, 100,  100,   0,  20
virt_type "Emulated GPU",    100, 100,   0,   90, 100,  20
virt_type "Assigned GPU",     90,  90,  90,   90,  30,   0
virt_type "VGPU",             50,  70,  70,   70,  70,  70
virt_type "VirtIO GPU",      100,  50,  50,   90,  90,  50


virt_type_hsv -> 0.0
virt_type Type, HW, SW, Perf, Sec, Flex, Scal ->
// ----------------------------------------------------------------------------
//   Draw a star diagram for the given type
// ----------------------------------------------------------------------------
    base_slide "Tradeoffs for " & Type,
        title Type
        subtitle "Tradeoffs, tradeoffs, it's all about tradeoffs"
        stepping
        picture
            consideration -12, "Hardware availability"
            consideration -11, "Software availability"
            consideration -10, "Performance"
            consideration -9, "Security"
            consideration -8, "Flexibility"
            consideration -7, "Scalability"

            interpolate 5%, page_number * 60, virt_type_hsv
            line_color_hsv virt_type_hsv, 80%, 70%, 90%
            color_hsv virt_type_hsv, 40%, 70%, 60%
            line_width 6
            path
                consideration_score 0, 0.0, HW
                consideration_score 1, 0.0, SW
                consideration_score 2, 0.0, Perf
                consideration_score 3, 0.0, Sec
                consideration_score 4, 0.0, Flex
                consideration_score 5, 0.0, Scal
                close_path


// ----------------------------------------------------------------------------
base_slide "Comparison - Conclusion",
// ----------------------------------------------------------------------------
    title "Conclusion: It's complicatedâ„¢"
    subtitle "There is no ""one-size-fits-all"" solution today"
    stepping
    picture
        frame_texture 1600, 700,
            color "#23355D" // Facebook blue
            rectangle 0, 0, 1600, 700
            radial_gradient 128, 128, 128, 256, 256,
                gradient_color 0%, "#3A5896"
                gradient_color 50%, "#344A80"
                gradient_color 100%, "#23355D"
            color "white"
            circle -400, 0, 800
            texture ""
            for I in -3..3 loop
                text_box -300, 0 - 80 * I, 400, 100,
                    font "Facebook Letter Faces", 50, bold
                    color "white", exp(-I^2*0.2)
                    text comparison_vm_text I
            show 1.0 - sin(10 * page_time)^2 * exp(-page_time * 1.5)
            rectangle -550, 29, 40, 40
            image -550, 40, 20%, 20%, "CheckMark.png"
            comparison_vm_text -3 -> "Native"
            comparison_vm_text -2 -> "Virtualized"
            comparison_vm_text -1 -> "Paravirtualized"
            comparison_vm_text  0 -> "It's complicated"
            comparison_vm_text  1 -> "GPU accelerated"
            comparison_vm_text  2 -> "In a container"
            comparison_vm_text  3 -> "Migrated"
        rectangle 0, 50, 1600, 700


consideration_score Cons, Current, Value ->
// ----------------------------------------------------------------------------
//   Indicate a color-coded score, where 0 = RED and 100 = GREEN
// ----------------------------------------------------------------------------
    interpolate 5%, Value, Current
    line_to 550 * sin(pi*Cons/3)*(Current%+0.2)/1.4, 50 + 300*cos(pi*Cons/3)*(Current%+0.2)/1.4


consideration Index:integer, Body ->
// ----------------------------------------------------------------------------
//   A text box representing one of the considerations being compared
// ----------------------------------------------------------------------------
    locally
        show fade_at(page_time, 0.5 * Index + 1)
        locally
            line_color "#444", fade_at(page_time, 0.5 * Index + 1.25)
            path
                move_to 0, 50
                line_to 550 * sin(pi * Index / 3), 50 + 300*cos(pi * Index / 3)
            line_color "#AAA", fade_at(page_time, 0.5 * Index + 1.6)
            path
                move_to 471 * sin(pi * Index / 3), 50 + 257*cos(pi * Index / 3)
                line_to 471 * sin(pi * (Index+1) / 3), 50 + 257*cos(pi * (Index+1) / 3)
        translate 600 * sin(pi * Index / 3), 50 + 350*cos(pi * Index / 3), 0
        text_box 0, 0, 250, 200,
            style "consideration"
            render Body
